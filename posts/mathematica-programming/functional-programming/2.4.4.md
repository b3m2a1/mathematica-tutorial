Title: Nest / NestList
Authors: 
Categories: mathematica-programming,functional-programming
Date: 2017-12-28 02:35:43
ID: 2.4.4
Modified: 2017-12-28 02:35:43
Path: Mathematica Programming/Functional Programming
Slug: nest--nestlist
Tags: functional-programming,nest,nestlist

<a id="nest--nestlist" style="width:0;height:0;margin:0;padding:0;">&zwnj;</a>

## Nest / NestList

### Nest

Often instead of mapping a function over a list we want to map a function in a nested fashion, taking the previous result as the argument of our next call. For this, there is  ```Nest``` . We could, for instance, implement a random-walk function using this.

First the random vector:

	randomVector =
	  Compile[{{mag, _Real}},
	    mag*Normalize@RandomReal[{-1, 1}, 3]
	    ];

Then the random walk starting from a point:

	randomWalk[start_, n_, stepSize_] := 
	  Nest[randomVector[stepSize] + # &, start, n];
	randomWalk[{0, 0, 0}, 100, .1]

	(*Out:*)
	
	{-0.670126, 0.141086, -0.186138}

Do to this procedurally we would have had to have scoped it then looped:

	randomWalkP[start_, n_, stepSize_] :=
	  Module[{current = start, i},
	    For[i = 1, i <= n , i++, current += randomVector[stepSize]];
	    current
	    ];

	randomWalkP[{0, 0, 0}, 100, .1]

	(*Out:*)
	
	{-0.0149769, 0.994544, -0.0801959}

And then we can see the efficiency gains of the former:

	randomWalkP[{0, 0, 0}, 10^6, .1] //
	  AbsoluteTiming // First

	(*Out:*)
	
	2.59918

	randomWalk[{0, 0, 0}, 10^6, .1] //
	  AbsoluteTiming // First

	(*Out:*)
	
	1.82464

Small (most of the cost comes from the normalization in  ```randomVector``` , hence the  ```Compile```  call) but non-negligible.

### NestList

The gains are drastic if we want all of our walk positions. For this we use the corresponding  ```NestList```  function:

	randomWalk2[start_, n_, stepSize_] :=
	  
	  NestList[randomVector[stepSize] + # &, start, n];

	randomWalk2[{0, 0, 0}, 10^6, .1] // AbsoluteTiming // First

	(*Out:*)
	
	1.89242

	randomWalkP2[start_, n_, stepSize_] :=
	  Module[{path = {start}, i},
	    For[i = 1, i <= n , i++, 
	    AppendTo[path, randomVector[stepSize] + Last@path]];
	    path
	    ];

	randomWalkP2[{0, 0, 0}, 10^5, .1] //
	  AbsoluteTiming // First

	(*Out:*)
	
	27.373

Note that, not only does it take almost 15x as long, we accumulated an order of magnitude fewer points.

Let’s put this to good use, now, seeing where our random walks end up:

	randomWalkP2[{0, 0, 0}, 2.5*10^4, .1] // Mean // AbsoluteTiming

	(*Out:*)
	
	{1.82134, {7.57578, -5.39405, 2.32296}}

	randomWalk2[{0, 0, 0}, 25*10^3, .1] // Mean // AbsoluteTiming

	(*Out:*)
	
	{0.043608, {-6.01548, -0.633615, -3.30331}}

Both have traveled a considerable distance and using the vast efficiency gains from the  ```NestList```  we can actually see if this holds in general or if it’s just a spurious effect:

	Table[randomWalk2[{0, 0, 0}, 25*10^3, .1] // Mean // Norm, 100] // 
	  Mean // AbsoluteTiming

	(*Out:*)
	
	{4.53762, 8.44775}

So it does in fact seem that, on average, a random walk will leave its origin. Note that doing that checking the same thing procedurally would have take a number of minutes. We can do it in under five seconds.